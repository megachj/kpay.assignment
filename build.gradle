/* 아티팩트(프로젝트) 정보 */
group 'megachj.kpay'
version '1.0.0'
description = 'KakaoPay Assignment'

/* 변수 선언 */
ext {
    srcDir = 'src/main/java'
    resourceDir = 'src/main/resources'
    profile = 'local'
}

if (project.hasProperty('profile')) {
    ext.profile = project.getProperties().get('profile')
}

/* 플러그인 */
apply plugin: 'java'                    // java build
apply plugin: 'application'             // mainClassName 지정
apply plugin: 'org.springframework.boot'// spring-boot

/* 외부 플러그인 설정 */
buildscript {
    // 외부 플러그인 저장소
    repositories {
        mavenCentral()
    }
    // 외부 플러그인 목록
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.5.22.RELEASE'
    }
}

/* plugin: application 설정 */
mainClassName = 'megachj.kpay.assignment.ApplicationServer'

/* plugin: java 설정 */
sourceCompatibility = JavaVersion.VERSION_1_8

jar {
    // MANIFEST.MF 파일에 들어갈 내용 추가
    manifest{
        attributes 'Implementation-Title': project.name
        attributes 'Implementation-Version': version
        attributes 'description': description
        attributes 'Main-Class': mainClassName
        attributes 'Timestamp': getCurrentTimestamp()
    }
    archiveName = 'megachj-kpay-assignment.jar'
}

// .jar 파일에 같이 묶일 파일들 설정
sourceSets {
    main {
        java {
            srcDir "${srcDir}"
        }
        resources {
            srcDir "${resourceDir}"
        }
    }
}

/* plugin: springboot 설정 */
bootRun {
    systemProperty "spring.profiles.active", "${profile}"
}

/* 메서드 선언 */
def getCurrentTimestamp()
{
    def date = new Date()
    return date.format('yyyy-MM-dd HH:mm:ss')
}

/* 의존성 라이브러리 저장소 */
repositories {
    mavenCentral()
}

/* 의존성 라이브러리 */
dependencies {
    // Spring
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile ('org.springframework.boot:spring-boot-starter-jdbc') {
        exclude group: 'org.apache.tomcat', module: 'tomcat-jdbc'
    }

    compile 'org.springframework.boot:spring-boot-starter-data-jpa'
    compileOnly 'org.springframework.boot:spring-boot-configuration-processor'

    // Etc
//    compile 'org.hibernate:hibernate-entitymanager:5.4.18.Final'
    compile 'com.h2database:h2:1.4.200'
    compile 'com.zaxxer:HikariCP:3.4.5'
    compileOnly 'org.projectlombok:lombok:1.16.18'

    // Test
    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test'
}